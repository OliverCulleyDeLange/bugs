plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

android {
    namespace 'com.example.proto'
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            proto {
                srcDir "${rootProject.projectDir}/protos"
            }
        }
    }
}

dependencies {
    api 'pro.streem.pbandk:pbandk-runtime-jvm:0.12.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1"
}


protobuf {
// Uncomment to place generated kotlin protos in protos/gen - doesn't seem to affect the bug though
//    generatedFilesBaseDir = "$projectDir/gen"
    protoc {
        /** Use a downloaded version of protoc so you don't have to download and install on your machine */
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.11.1:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.11.1'
        }
    }
    plugins {
        kotlin {
            artifact = 'pro.streem.pbandk:protoc-gen-pbandk-jvm:0.12.1:jvm8@jar'
        }
    }
    generateProtoTasks {
        ofNonTest().each { task ->
            task.group = "proto"
            // The generateProto task does not seem to properly clean its previously generated outputs.
            // See https://github.com/google/protobuf-gradle-plugin/issues/332
            // See https://github.com/google/protobuf-gradle-plugin/issues/331
            task.doFirst {
                delete task.outputs
            }
            task.builtins {
                remove java // Don't output generated java proto bindings. We only want the kotlin
            }
            task.plugins {
                kotlin {
                    option 'log=debug'
                    option 'DEBUG=true'
                }
            }
        }
    }
}

//clean {
//    delete protobuf.generatedFilesBaseDir
//}
