plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'io.fabric'
    id 'com.google.gms.google-services'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.mikepenz.aboutlibraries.plugin'
}

def gitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()
def pipelineId = System.getenv("CI_PIPELINE_IID")
def version = "1.4.0"
def versionName = "$version-${pipelineId ?: gitHash}"
def versionCode = Integer.parseInt(pipelineId ?: "1")
println "Version: $versionName, code: $versionCode"

project.version = versionName

android {
//    signingConfigs {
//        release {
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['keyPassword']
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//        }
//    }
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    ndkVersion "21.1.6352462"

    defaultConfig {
        applicationId "com.example"
        minSdkVersion 23
        targetSdkVersion 30
        setVersionCode(versionCode)
        setVersionName(versionName)
        buildConfigField "String", "GitHash", "\"${gitHash}\""

    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }
//    assetPacks = [":app_assets"]

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.release
            manifestPlaceholders = [crashlyticsEnabled: true]
        }
        debug {
            manifestPlaceholders = [crashlyticsEnabled: false]
            //https://developer.android.com/studio/build/application-id
            applicationIdSuffix ".debug"
        }
    }
    sourceSets {
        test {
            java.srcDirs += "src/sharedTest/java"
        }
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    viewBinding {
        enabled = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            useJUnitPlatform()
        }
        animationsDisabled = true
    }
    androidExtensions {
        experimental = true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    // https://developer.android.com/jetpack/androidx/versions
    // https://developer.android.com/kotlin/ktx#use-ktx

    def fragmentVersion = "1.2.5"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"
    // FIXME Warning?
    // https://stackoverflow.com/questions/62719924/android-fragment-testing-dependency-causes-release-build-to-fail-compilation
    // https://github.com/android/android-test/issues/294#issuecomment-653867249
    testImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    implementation "androidx.core:core-ktx:1.3.1"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Google services
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    implementation 'com.google.android.play:core:1.8.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-messaging:20.1.2' // Needed for intercom push notifications
    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // room database
    def roomVersion = "2.2.4"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    def work_version = "2.3.3"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-rxjava2:$work_version"

    implementation 'com.google.android.exoplayer:exoplayer:2.12.0'

    // Koil - Image loading
    implementation "io.coil-kt:coil:0.8.0"

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    // Facebook Login only
    implementation 'com.facebook.android:facebook-login:5.15.3'

    // Koin
    def koinVersion = "2.0.1"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

//    implementation "org.koin:koin-androidx-scope:$koinVersion"
//    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
//    implementation "org.koin:koin-androidx-fragment:$koinVersion"

    // RxJava
    def rxJavaVersion = '2.2.12'
    def rxKotlinVersion = '2.4.0'
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"

    // replaying share
    implementation 'com.jakewharton.rx2:replaying-share-kotlin:2.2.0'

    // RxPermissions
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // rx relay
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    //rx bindings
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0'

    // rxAndroidBle
    api "com.polidea.rxandroidble2:rxandroidble:1.10.5"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'fr.bipi.treessence:treessence:0.3.0'

    // groupie for lists
    implementation "com.xwray:groupie:2.8.0"
    implementation "com.xwray:groupie-kotlin-android-extensions:2.8.0"

    // Auto dispose
    def autoDisposeVersion = '1.4.0'
    implementation "com.uber.autodispose:autodispose:$autoDisposeVersion"
    implementation "com.uber.autodispose:autodispose-android:$autoDisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:$autoDisposeVersion"

    // battery view
    implementation 'eo.view:batterymeter:2.0.0'

    // material dialogs
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:datetime:3.3.0'
    implementation 'com.afollestad.material-dialogs:lifecycle:3.2.1'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.2.1'
    implementation 'com.afollestad.material-dialogs:input:3.2.1'

    implementation "com.instabug.library:instabug:9.0.5"

    // stetho for debugging
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Rx phone sensors. Don't upgrade! See https://github.com/pwittchen/ReactiveSensors/blob/RxJava3.x/CHANGELOG.md
    // 0.2.0 is last verion that uses rx2.0
    implementation 'com.github.pwittchen:reactivesensors-rx2:0.2.0'

    // three ten
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // Graph charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // temporary range bar
    implementation 'io.apptik.widget:multislider:1.3'

    // intercom intergration
    implementation 'io.intercom.android:intercom-sdk:6.0.0'

    // page indicator
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'

    // circle image view
    implementation 'com.mikhaellopez:circularimageview:4.1.0'

    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.6'

    /* Fast scrolling */
    implementation 'me.zhanghai.android.fastscroll:library:1.1.4'

    implementation "com.mikepenz:aboutlibraries-core:8.3.1"
    implementation "com.mikepenz:aboutlibraries:8.3.1"
    // TEST
    def junitVersion = "5.7.0"
    testImplementation("org.junit.jupiter:junit-jupiter:$junitVersion")
    testCompileOnly('junit:junit:4.13') // To allow running JUnit 4 Tests
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:$junitVersion")

    def kotestVersion = '4.2.5'
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotestVersion"
    // Assertions
    testImplementation "org.assertj:assertj-core:3.14.0"

    // Mockk
    testImplementation "io.mockk:mockk:1.10.0"

    // Mockito
    testImplementation "org.mockito:mockito-inline:3.1.0"
    testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    testImplementation 'org.threeten:threetenbp:1.4.0'


    def testVersion = "1.2.0"
    androidTestImplementation "androidx.test:runner:$testVersion"
    androidTestImplementation "androidx.test:rules:$testVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    //https://github.com/mockk/mockk/blob/master/ANDROID.md
    androidTestImplementation "io.mockk:mockk-android:1.10.0"

    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'

    androidTestImplementation 'com.agoda.kakao:kakao:2.3.3' // Espresso page objects

    def espressoVersion = "3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
}
